<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Who would win in a fight (according to AI)?</title>
    <meta name="description" content="Ask AI who would in a fight, race, or any other competition.">
    <meta name="keywords" content="Who would win, wwwai, ai fights, ai competition, ai versus, gpt-3.5-turbo, gpt-4, AI">
    <meta name="author" content="Benji Trosch">
    <link rel="canonical" href="https://whowouldwin.ai/">
    <link rel="stylesheet" href="src/styles.css" preload>
    <script id="blit-f" type="x-shader/x-fragment">
        // Render final fire, mapping heat to color ramp
        varying mediump vec2 texVarying;
        uniform sampler2D tex;
        uniform sampler2D palette;
        
        void main()
        {
            lowp float s = texture2D(tex, texVarying).x;
            gl_FragColor = texture2D(palette, vec2(s, 0.5));
        }
    </script>
    <script id="blit-v" type="x-shader/x-vertex">
        attribute vec4 position;
        attribute vec2 texcoord;
        
        varying vec2 texVarying;
        
        void main()
        {
            gl_Position = position;
            texVarying = texcoord;
        }
    </script>
    <script id="convolve-f" type="x-shader/x-fragment">
        #extension GL_OES_standard_derivatives : enable
        precision highp float;
        // Blur and convect the heat texture
        
        //
        // Description : Array and textureless GLSL 2D/3D/4D simplex 
        //               noise functions.
        //      Author : Ian McEwan, Ashima Arts.
        //  Maintainer : ijm
        //     Lastmod : 20110822 (ijm)
        //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
        //               Distributed under the MIT License. See LICENSE file.
        //               https://github.com/ashima/webgl-noise
        // 
        
        vec3 mod289(vec3 x) {
          return x - floor(x * (1.0 / 289.0)) * 289.0;
        }
        
        vec4 mod289(vec4 x) {
          return x - floor(x * (1.0 / 289.0)) * 289.0;
        }
        
        vec4 permute(vec4 x) {
             return mod289(((x*34.0)+1.0)*x);
        }
        
        vec4 taylorInvSqrt(vec4 r)
        {
          return 1.79284291400159 - 0.85373472095314 * r;
        }
        
        float snoise(vec3 v)
        { 
          const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
          const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);
        
        // First corner
          vec3 i  = floor(v + dot(v, C.yyy) );
          vec3 x0 =   v - i + dot(i, C.xxx) ;
        
        // Other corners
          vec3 g = step(x0.yzx, x0.xyz);
          vec3 l = 1.0 - g;
          vec3 i1 = min( g.xyz, l.zxy );
          vec3 i2 = max( g.xyz, l.zxy );
        
          //   x0 = x0 - 0.0 + 0.0 * C.xxx;
          //   x1 = x0 - i1  + 1.0 * C.xxx;
          //   x2 = x0 - i2  + 2.0 * C.xxx;
          //   x3 = x0 - 1.0 + 3.0 * C.xxx;
          vec3 x1 = x0 - i1 + C.xxx;
          vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
          vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y
        
        // Permutations
          i = mod289(i); 
          vec4 p = permute( permute( permute( 
                     i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                   + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
                   + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));
        
        // Gradients: 7x7 points over a square, mapped onto an octahedron.
        // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
          float n_ = 0.142857142857; // 1.0/7.0
          vec3  ns = n_ * D.wyz - D.xzx;
        
          vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)
        
          vec4 x_ = floor(j * ns.z);
          vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)
        
          vec4 x = x_ *ns.x + ns.yyyy;
          vec4 y = y_ *ns.x + ns.yyyy;
          vec4 h = 1.0 - abs(x) - abs(y);
        
          vec4 b0 = vec4( x.xy, y.xy );
          vec4 b1 = vec4( x.zw, y.zw );
        
          //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
          //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
          vec4 s0 = floor(b0)*2.0 + 1.0;
          vec4 s1 = floor(b1)*2.0 + 1.0;
          vec4 sh = -step(h, vec4(0.0));
        
          vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
          vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;
        
          vec3 p0 = vec3(a0.xy,h.x);
          vec3 p1 = vec3(a0.zw,h.y);
          vec3 p2 = vec3(a1.xy,h.z);
          vec3 p3 = vec3(a1.zw,h.w);
        
        //Normalise gradients
          vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
          p0 *= norm.x;
          p1 *= norm.y;
          p2 *= norm.z;
          p3 *= norm.w;
        
        // Mix final noise value
          vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
          m = m * m;
          return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                        dot(p2,x2), dot(p3,x3) ) );
          }
        
        // -------------------------------------------------------
        
        
        varying mediump vec2 texVarying;
        varying mediump vec2 coolcoord;
        uniform sampler2D tex;
        uniform sampler2D cooltex;
        uniform sampler2D noisetex;
        uniform mediump vec2 hpel;
        uniform mediump vec2 offset;
        uniform mediump vec2 up;
        uniform highp float time;
        
        void main()
        {
            highp vec3 noisecoord = vec3(8.0 * texVarying.x,
                                         8.0 * texVarying.y,
                                         2.0 * time);
            highp float noise = snoise(noisecoord);
            //noise += 0.25 * snoise(2.5 * noisecoord);
            highp vec2  curl  = hpel * vec2(dFdy(noise), -dFdx(noise));
            mediump vec2 tcoord = texVarying - up + (3.0 * curl);
            highp float a = 0.0;
        
            a += texture2D(tex, tcoord + vec2(-hpel.x, 0.0)).r;
            a += texture2D(tex, tcoord + vec2(hpel.x, 0.0)).r;
            a += texture2D(tex, tcoord + vec2(0.0, -hpel.y)).r;
            a += texture2D(tex, tcoord + vec2(0.0, hpel.y)).r;
            a *= 0.25;
            lowp float coolfact = 0.03;
            a -= coolfact * texture2D(cooltex, coolcoord).x;
            gl_FragColor =  vec4(a);
        }
    </script>
    <script id="convolve-v" type="x-shader/x-vertex">
        attribute vec4 position;
        attribute vec2 texcoord;
        varying vec2 texVarying;
        varying vec2 coolcoord;
        
        uniform mediump vec2 hpel;
        uniform mediump vec2 up;
        uniform mediump vec2 offset;
        
        void main()
        {
            gl_Position = position;
            texVarying = texcoord;
            coolcoord = texcoord - offset;
        }
    </script>
    <script id="hotparts-f" type="x-shader/x-fragment">
        // Add burning fuel to the heat texture
        uniform sampler2D flickertex;
        uniform sampler2D hottex;
        varying mediump vec2 texVarying;
        varying mediump vec2 flickerVarying;
        
        void main()
        {
            lowp float basecol = texture2D(hottex, texVarying).r;
            lowp float flicker = texture2D(flickertex, flickerVarying).r;
            lowp float col = basecol * flicker;
            gl_FragColor =  vec4(col, col, col, basecol);
        }
    </script>
    <script id="hotparts-v" type="x-shader/x-vertex">
        attribute vec4 position;
        attribute vec2 texcoord;
        varying vec2 flickerVarying;
        varying vec2 texVarying;
        uniform vec2 randvec;
        
        void main()
        {
            gl_Position = position;
            texVarying = texcoord;
            flickerVarying = randvec + texVarying;
        }
    </script>
    <script id="paint-f" type="x-shader/x-fragment">
        // Paint strokes to the burning fuel texture
        void main()
        {
            gl_FragColor = vec4(1.0);
        }
    </script>
    <script id="paint-v" type="x-shader/x-vertex">
        attribute vec4 position;
        
        void main()
        {
            gl_Position = position;
        }
    </script>
</head>
<body>
    <header>
        <h1 class="hidden">Who would win?</h1>
    </header>
    <section>
        <aside aria-label="Website URL">whowouldwin.ai</aside>
        <form>
            <label>
                Who would win a
                <span>
                    <select name="select-competition">
                        <option>Fight</option>
                        <option>100-metre dash</option>
                        <option>Rap battle</option>
                        <option>Slam dunk contest</option>
                    </select>
                    ?
                </span>
            </label>
            <div class="input-group">
                <input name="first" type="text" minlength="1" maxlength="32" autocomplete="off" required />
                <span>vs.</span>
                <input name="second" type="text" minlength="1" maxlength="32" autocomplete="off" required />
            </div>
            <button type="submit" disabled>Ask ChatGPT</button>
        </form>
    </section>
    <section class="hidden">
        <h2 class="hidden">Your answer will go here</h2>
        <div id="answer" role="region" aria-live="polite" tabindex="-1"></div>
    </section>
    <footer>
        <small>Made in a few hours by <a href="https://github.com/benjitrosch/whowouldwin" target="_blank">Benji</a></small>
    </footer>
    <canvas id="c" width="256" height="256" tabindex="-1"></canvas>
    <script type="module" src="src/index.ts" defer></script>
</body>
</html>
